plugins{
    id "java"
}

group = "stealthpath"
version = "2.0.2"

dependencies{
    // Build against this Mindustry source checkout.
    compileOnly project(":core")
}

tasks.withType(JavaCompile).configureEach{
    options.encoding = "UTF-8"
}

jar{
    // A Mindustry Java mod is a zip/jar archive with classes + resources.
    // Use .zip extension for distribution.
    archiveFileName = "${project.name}.zip"
}

// Keep a copy at the repo root for easy install.
tasks.register("copyZipToRoot", Copy){
    // Root contains Gradle locks; disable state tracking to avoid snapshot failures.
    doNotTrackState("Copies into root directory which may contain unreadable Gradle lock files.")
    dependsOn tasks.named("jar")
    from(tasks.named("jar").flatMap{ it.archiveFile })
    // "root" here means the directory alongside Mindustry-main/Mindustry-master.
    into(rootDir.parentFile)
}

tasks.named("jar").configure{
    finalizedBy tasks.named("copyZipToRoot")
}
